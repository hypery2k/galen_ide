grammar com.galenframework.Spec with org.eclipse.xtext.xbase.Xbase hidden(WS, ML_COMMENT, SL_COMMENT)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate spec "http://www.galenframework.com/Spec"

Model:
	{Model}
	objects=Objects?
	importSection+=Import*
	layoutCheckSection+=Section*;

Objects:
	'@objects'
	BEGIN
	(elements+=Element)+
	END;

Import:
	'@import' name=STRING;

Section:
	'=' name=STRING '='
	BEGIN
	rules+=LayoutRule*
	taggedSection=TaggedSection?
	END;

TaggedSection:
	'@on' tag=STRING
	sections+=Section+;

LayoutRule:
	ref=[Element] ':'
	BEGIN
	(references+=SpecsReference)+
	(sections+=Section)*
	END;

SpecsReference:
	{SpecsReference}
	SimpleSpecsReference | ComplexSpecsReference;

SimpleSpecsReference:
	'Visible' | 'Absent';

ComplexSpecsReference:
	{ComplexSpecsReference} ((('near' | 'below' | 'above') INT 'to' INT 'px') |
	('aligned horizontally' ('all' | 'top' | 'bottom' | 'centered') (ref=[Element])? (INT 'px')?) |
	('aligned vertically' ('all' | 'left' | 'bottom' | 'right') (ref=[Element])? (INT 'px')?) |
	('height' ('<' | '>') (INT 'px' | 'screen/height')) |
	('width' ('<' | '>') (INT 'px' | 'screen/width@')));

Element returns Element:
	{Element}
	name=ID (type=('id' | 'xpath' | 'css'))? selector=Selector
	BEGIN
	(children+=Element)*
	END;

Selector returns ecore::EString:
	('.' | '//' | '#')? (QualifiedName | '[' | ']' | '@' | '*' | '=' | '\'' | '"') Selector?;

QualifiedName:
	ID ('.' ID)*;

terminal ID:
	('a'..'z' | 'A'..'Z' | '_' | '-') ('a'..'z' | 'A'..'Z' | '_' | '-' | '*' | '0'..'9')*;

	// Single line comment
terminal SL_COMMENT:
	'#' WS !('\n' | '\r')* ('\r'? '\n')?;

terminal NL:
	('\r' | '\n')+ ('\t' | ' ')+;

terminal WS:
	('\r' | '\n' | '\t' | ' ')+;
	// The following synthetic tokens are used for the indentation-aware blocks
terminal BEGIN:
	'synthetic:BEGIN'; // increase indentation
terminal END:
	'synthetic:END'; // decrease indentation
