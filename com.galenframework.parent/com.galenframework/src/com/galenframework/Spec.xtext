grammar com.galenframework.Spec hidden(SL_COMMENT, WS) // with org.eclipse.xtext.common.Terminals
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
generate spec "http://www.galenframework.com/Spec"

Model:
	objectSection=Object
	importSection+=Import*
	layoutCheckSection+=Section*;

Object:
	'@objects' objects=ObjectDeclaration;

ObjectDeclaration:
	BEGIN
	(elements+=Element)+
	END;

Import:
	'@import' name=String;

Section:
	'=' name=String '='
	ref=[Element] ':'
	(BEGIN
	(references+=SpecsReference)+
	(sections+=Section)*
	END) |
	(BEGIN
	'@on' name=String
	BEGIN
	(references+=SpecsReference)+
	(sections+=Section)*
	END
	END);

SpecsReference:
	SimpleSpecsReference | ComplexSpecsReference;

SimpleSpecsReference:
	'Visible' | 'Absent';

ComplexSpecsReference:
	(('near' | 'below' | 'above') Integer 'to' Integer 'px') |
	('aligned horizontally' ('all' | 'top' | 'bottom' | 'centered') (ref=[Element])? (Integer 'px')?) |
	('aligned vertically' ('all' | 'left' | 'bottom' | 'right') (ref=[Element])? (Integer 'px')?) |
	('height' ('<' | '>') (Integer 'px' | 'screen/height')) |
	('width' ('<' | '>') (Integer 'px' | 'screen/width@'));

Element returns Element:
	name=ID (type=('id' | 'xpath' | 'css'))? selector=Selector ('\n' | '\r')
	(BEGIN
	(elements+=Element)+
	END)?;

Selector returns ecore::EString:
	('.' | '//' | '#')? (QualifiedName | '[' | ']' | '@' | '*' | '=' | '\'' | '"') Selector?;

String returns ecore::EString:
	(WS? ID+ WS?)+;

Integer returns ecore::EInt:
	NUMBER+;

QualifiedName:
	ID ('.' ID)*;

terminal NUMBER:
	('1'..'9') ('0'..'9')*;

terminal ID:
	('a'..'z' | 'A'..'Z' | '_' | '-') ('a'..'z' | 'A'..'Z' | '_' | '-' | '*' | '0'..'9')*;

terminal WS:
	(' ' | '\t')+;

terminal NL:
	('\r' | '\n')+ WS*;

	// Single line comment
terminal SL_COMMENT:
	'#' WS !('\n' | '\r')*;

	// The following synthetic tokens are used for the indentation-aware blocks
terminal BEGIN:
	'synthetic:BEGIN'; // increase indentation
terminal END:
	'synthetic:END'; // decrease indentation
